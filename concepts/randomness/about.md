In Elixir, to choose a random element from an enumerable data structure (e.g. list, range), we use [`Enum.random/1`][enum-random]. This function will pick a single element, with every element having equal probability of being picked. Picking a single random value from a range is executed in constant time, without traversing the whole range.

## Random integers and letters

- Choosing a random integer
  ```elixir
  Enum.random(1..10)
  # => 7
  ```
- Choosing a random letter as a string
  ```elixir
  <<Enum.random(?A..?Z)>>
  # => "K"
  ```
- Choosing a random letter as a charlist
  ```elixir
  [Enum.random(?A..?Z)]
  # => 'J'
  ```

To choose more than one element from an enumerable data structure, use [`Enum.take_random/2`][enum-take-random]. To sort an enumerable data structure in a random order, use [`Enum.shuffle/1`][enum-shuffle].

## Random floats

Elixir does not have its own functions for picking a random float. To do that, we have to use Erlang's [`rand`][erl-rand] module, which also happens to be what `Enum.random` uses under the hood.

- Choosing a random float (equal to or greater than 0.0 and less than 1.0):

  ```elixir
  :rand.uniform()
  # => 0.275329955448815
  ```

- Choosing a random float (equal to or greater than `a` and less than `b`):
  ```elixir
  a = 13
  b = 20
  :rand.uniform() * (b - a) + a
  # => 18.96815556890448
  ```

## Normal distribution

All the functions mentioned above choose a random value with every possible value having the same probability of being chosen. That's called a _uniform distribution_. The `rand` module also offers [`normal/0`][erl-rand-normal-0] and [`normal/2`][erl-rand-normal-1] functions that choose a random float with a [normal distribution][normal-distribution].

## Seed

It's important to understand that the "random" values are generated by a [_pseudo_ random number generator][prng]. A PRNG is an algorithm that, given an initial value called a _seed_, will generate a sequence of numbers that seems random, but isn't random at all.

This fact might be useful for debugging. You can force Erlang's (and thus Elixir's) pseudo random number generator to produce the same sequence values by resetting its seed using [`:rand.seed/2`][rand-seed-2]. The first argument is a specific PRNG algorithm, and the second a three-integer-tuple - the seed.

```elixir
:rand.seed(:exsss, {1, 2, 3})
Enum.take_random(100..999, 3)
# => [680, 127, 622]

:rand.seed(:exsss, {1, 2, 3})
Enum.take_random(100..999, 3)
# => [680, 127, 622]
```

## Cryptography

The random values returned by `Enum.random` and the Erlang's `rand` module are not [cryptographically secure][cryptographically-secure-rng]. Do not use them to generate cryptographical keys, passwords, or nonces. For this use case, there's Erlang's [`:crypto.strong_rand_bytes/1`][erl-crypto-strong-rand-bytes].

[enum-random]: https://hexdocs.pm/elixir/Enum.html#random/1
[enum-take-random]: https://hexdocs.pm/elixir/Enum.html#take_random/2
[enum-shuffle]: https://hexdocs.pm/elixir/Enum.html#shuffle/1
[erl-rand]: http://erlang.org/doc/man/rand.html
[erl-crypto-strong-rand-bytes]: http://erlang.org/doc/man/crypto.html#strong_rand_bytes-1
[cryptographically-secure-rng]: https://en.wikipedia.org/wiki/Cryptographically_secure_pseudorandom_number_generator
[normal-distribution]: https://en.wikipedia.org/wiki/Normal_distribution
[erl-rand-normal-0]: http://erlang.org/doc/man/rand.html#normal-0
[erl-rand-normal-2]: http://erlang.org/doc/man/rand.html#normal-2
[prng]: https://en.wikipedia.org/wiki/Pseudorandom_number_generator
[rand-seed-2]: http://erlang.org/doc/man/rand.html#seed-2
